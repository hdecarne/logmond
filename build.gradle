// Plugins
plugins {
	id "java-library"
	id "jacoco"
	id "eclipse"
	id "de.carne.java-tools" version "0.7.1"
	id "org.sonarqube" version "2.7"
}
// Build helpers
def snapshotBuild = project.version.endsWith("-SNAPSHOT")

def resolveProperty(propertyName, envName) {
	def resolved = (project.findProperty(propertyName) ?: System.getenv(envName));

	if(resolved == null) {
		throw new GradleException("Missing property/environment variable: '${propertyName}'/'${envName}'")
	}
	return resolved; 
}

// Project
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
	if(snapshotBuild) {
		mavenLocal()
		maven { url "https://oss.jfrog.org/libs-snapshot/" }
	}
	jcenter()
	maven { url "https://oss.jfrog.org/libs-release/" }
}

dependencies {
	implementation(group: "de.carne.common", name: "java-lwjsd-api", version: "0.4.0-SNAPSHOT")
	implementation(group: "org.junit.jupiter", name: "junit-jupiter-api", version: "5.4.0-M1")
	testRuntimeOnly(group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "5.4.0-M1")
	testImplementation(group: "de.carne.common", name: "java-lwjsd-runtime", version: "0.4.0-SNAPSHOT")
}

test {
	useJUnitPlatform()
}

jacoco {
	toolVersion = "0.8.2"
}

jacocoTestReport {
	reports {
		xml.enabled false
		html.enabled true
		html.destination file("${buildDir}/reports/jacoco")
		csv.enabled false
	}
}

eclipse {
	classpath {
		downloadSources = true
		downloadJavadoc = true
		file {
			whenMerged {
				def eeaPath = project.findProperty("eclipse.eeaPath");
				 
				entries.forEach {
					if(it.path.startsWith("org.eclipse.jdt.launching.JRE_CONTAINER")) {
						if(eeaPath != null) {
							it.entryAttributes.put("annotationpath", eeaPath)
						}
					} else if(it.path.contains("/caches/modules-2/")) {
						if(eeaPath != null) {
							it.entryAttributes.put("annotationpath", eeaPath)
						}
					}
				}
			}
		}
	}
}
